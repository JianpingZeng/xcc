/**
 * option illustration.
 */
options {
    STATIC = false;
    DEBUG_PARSER = true;
    UNICODE_INPUT = true;
    JDK_VERSION = "1.7";
}

PARSER_BEGIN(Parser)

package parser;

import ast.*;
import ast.Tree.*;
import type.*;
import utils.*;
import exception.*;
import java.util.*;
import java.io.*;

/**
 * The parser that parses the micro-c source file.
 * It maps a token sequence into an abstract syntax
 * tree which operates by LR grammar.
 * @version 1.0
 */
public class Parser {

	/**
     * The factory to be used for construction of abstract syntax tree.
     */
    private TreeMaker F;

	/**
	 * Default supported character encoding style.
	 */
    static final public String SOURCE_ENCODING = "UTF-8";

    /**
     * A name table.
     */
    private Name.Table names;

    /**
     * A logger to be error reporting.
     */
    private Log log;  
    
	/**
	 * The current token.
	 */
	private Token t;

	private static final Context.Key parserKey = new Context.Key();
	
	/**
	 * Constructs a parser.
	 * 
	 * @param input	the input stream
	 * @param debug		whether debug  or not 
	 * @param context	A context object
	 */
	public Parser(InputStream input, boolean debug, Context context)
	{
		this(input, SOURCE_ENCODING);
		context.put(parserKey, this);
		this.F = TreeMaker.instance(context);
		this.log = Log.instance(context);
		this.names = Name.Table.instance(context);
		if (debug)
		{
			enable_tracing();
		}
		else
		{
			disable_tracing();
		}
	}

	/**
	 * Constructor with specifid file.
	 * @throws FileNotFoundException 
	 * @throws UnsupportedEncodingException 
	 */
	public Parser(String filename, boolean debug, Context context) 
			throws UnsupportedEncodingException, FileNotFoundException 
	{
		this(new FileInputStream(filename), debug, context);
	}

	/**
	 * Constructs a Integer literal node.
	 */
    private Literal integerNode(int pos, String image)
    {
        long i = integerValue(image);
        if (image.endsWith("L"))
        {
            return F.at(pos).Literal(Type.LONG, new Long(i));
        }
        else
        {
            return F.at(pos).Literal(Type.INT, new Integer((int)i));
        }
    }
        
	/**	
	 * A auxiliary function for extracting a integer or long value.
	 */	        
    private long integerValue(String image)
    {
        String s = image.replaceFirst("[UL]+", "");
        if (s.startsWith("0x") || s.startsWith("0X"))
        {
            return Long.parseLong(s.substring(2), 16);
        }
        else if (s.startsWith("0") && !s.equals("0"))
        {
            return Long.parseLong(s.substring(1), 8);
        }
        else
        {
            return Long.parseLong(s, 10);
        }
    }

	/**	
	 * A auxiliary function for extracting a character node.
	 */	 
    private long characterCode(String image) throws ParseException
    {
        String s = stringValue(image);
        if (s.length() != 1)
        {
            throw new Error("must not happen: character length > 1");
        }
        return (long)s.charAt(0);
    }

	/**	
	 * A auxiliary function for extracting a string value.
	 */	 
    private String stringValue(String _image) throws ParseException
    {
        int pos = 0;
        int idx;
        StringBuffer buf = new StringBuffer();
        String image = _image.substring(1, _image.length() - 1);

        while ((idx = image.indexOf("\\", pos)) >= 0)
        {
            buf.append(image.substring(pos, idx));
            if (image.length() >= idx + 4
                    && Character.isDigit(image.charAt(idx+1))
                    && Character.isDigit(image.charAt(idx+2))
                    && Character.isDigit(image.charAt(idx+3)))
            {
                buf.append(unescapeOctal(image.substring(idx+1, idx+4)));
                pos = idx + 4;
            }
            else {
                buf.append(unescapeSeq(image.charAt(idx+1)));
                pos = idx + 2;
            }
        }
        if (pos < image.length())
        {
            buf.append(image.substring(pos, image.length()));
        }
        return buf.toString();
    }


    private static final int charMax = 255;

    private char unescapeOctal(String digits) throws ParseException
    {
        int i = Integer.parseInt(digits, 8);
        if (i > charMax)
        {
            throw new ParseException(
                "octal character sequence too big: \\" + digits);
        }
        return (char)i;
    }



    private static final char bell = 7;
    private static final char backspace = 8;
    private static final char escape = 27;
    private static final char vt = 11;

    private char unescapeSeq(char c) throws ParseException
    {
        switch (c)
        {
            case '0': return '\0';
            case '"': return '"';
            // there is a bug exist in below code caused by Javacc 6.0
            // this bug is that there is redundancy white space in the end
            // of character literal encoded by utf-8.
            case '\'': return '\'';
            case 'a': return bell;
            case 'b': return backspace;
            case 'e': return escape;
            case 'f': return '\f';
            case 'n': return '\n';
            case 'r': return '\r';
            case 't': return '\t';
            case 'v': return vt;
            default:
                throw new ParseException("unknown escape sequence: \"\\" + c);
        }
    }

	/**
	 * Acquires the current position of current token.
	 */
	private int position()
	{
		// get the current token
		Token current = getToken(0);
		return Position.make(current.beginLine, current.beginColumn);
	}
}
PARSER_END(Parser)

/*
 * Lexical Rules
 */

// linear-white-spaces
// #@@range/lex_spaces{
SPECIAL_TOKEN: { <SPACES: ([" ", "\t", "\n", "\r", "\f"])+> }
//

// block comment
MORE: { <"/*"> : IN_BLOCK_COMMENT }
<IN_BLOCK_COMMENT> MORE: { <~[]> }
<IN_BLOCK_COMMENT> SPECIAL_TOKEN: { <BLOCK_COMMENT: "*/"> : DEFAULT }


// single line comment
SPECIAL_TOKEN: {
    <LINE_COMMENT: "//" (~["\n", "\r"])* ("\n" | "\r\n" | "\r")?>
}


// reserved words or keywords
TOKEN: {
      <VOID     : "void">
  	| <BYTE		:  "byte">
    | <CHAR     : "char">
    | <SHORT    : "short">
    | <INT      : "int">
    | <LONG     : "long">
    | <FLOAT	: "float">
    | <DOUBLE 	: "double">
    | <BOOL 	: "bool">
    | <STRUCT   : "struct">
    | <UNION    : "union">
    | <ENUM     : "enum">
    | <STATIC   : "static">
    | <EXTERN   : "extern">
    | <CONST    : "const">
    | <SIGNED   : "signed">
    | <TRUE_ 	: "true">
    | <FALSE_ 	: "false">
    | <UNSIGNED : "unsigned">
    | <IF       : "if">
    | <ELSE     : "else">
    | <SWITCH   : "switch">
    | <CASE     : "case">
    | <DEFAULT_ : "default">
    | <WHILE    : "while">
    | <DO       : "do">
    | <FOR      : "for">
    | <RETURN   : "return">
    | <BREAK    : "break">
    | <CONTINUE : "continue">
    | <GOTO     : "goto">
    | <TYPEDEF  : "typedef">
    | <IMPORT   : "import">
    | <SIZEOF   : "sizeof">
}

// IDENTIFIER
TOKEN: {
    <IDENTIFIER: ["a"-"z", "A"-"Z", "_"] (["a"-"z", "A"-"Z", "_", "0"-"9"])*>
}

// integer literals
TOKEN: {
    <INTEGER: ["1"-"9"] (["0"-"9"])* ("U")? ("L")?
            | "0" ["x", "X"] (["0"-"9", "a"-"f", "A"-"F"])+ ("L")?
            | "0" (["0"-"7"])* ("L")?
            >
}


// character literal
MORE: { <"'"> : IN_CHARACTER }                         // rule1
<IN_CHARACTER> MORE: {
      <~["'", "\\", "\n", "\r"]> : CHARACTER_TERM      // rule2
    | <"\\" (["0"-"7"]){3}>      : CHARACTER_TERM      // rule3
    | <"\\" ~[]>                 : CHARACTER_TERM      // rule4
}
<CHARACTER_TERM> TOKEN: { <CHARACTER: "'"> : DEFAULT } // rule5


// string literal
MORE: { <"\""> : IN_STRING }                           // rule1
<IN_STRING> MORE: {
      <(~["\"", "\\", "\n", "\r"])+>                   // rule2
    | <"\\" (["0"-"7"]){3}>                            // rule3
    | <"\\" ~[]>                                       // rule4
}
<IN_STRING> TOKEN: { <STRING: "\""> : DEFAULT }        // rule5



/**
 * The entry for parsing the c-flat source file. Then a tree that represents
 * the toplevel should be returned.
 */
public Tree.TopLevel compilationUnit():
{
    // two list that contain import clauses or type definition respectively
	List<Tree> importdecls, typedecls;
}
{
    {t = getToken(1);}
    importdecls = importDeclarations() typedecls = top_defs() <EOF>
    {
        importdecls.addAll(typedecls);
        int pos = position();
        return F.at(pos).TopLevel(importdecls);
    }
}


/**
 * <p>Parses the import clauses and returns a abstract syntax tree which served as
 * representation of import clauses.
 * <p> Extended BNF grammer;
 * <pre>
 * importDeclarations::
 *				import_stmt;
 *				importDeclarations
 * </pre>
 * @return 
 */
List<Tree> importDeclarations():
{
    List<Tree> imports = new ArrayList<Tree>();
    Tree impt;
}
{
    ( impt = import_statement() { imports.add(impt);} 
    )*
    {
        return imports;
    }
}


/**
 * Handles single import statement and returns a string representing the import
 * statement which consists of path name relative to directory of current project.
 * <pre>
 * import_statement::= IMPORT name SEMICOLON
 *						| IMPORT name DOT MULT SEMICOLON
 *
 * name	::=	simple_name
 *	|	qualified_name
 *	;
 * simple_name ::=	IDENTIFIER
 *	;
 * qualified_name ::=
 *		name DOT IDENTIFIER
 *	;
 * </pre>
 * @return
 */
Tree import_statement():
{
	Tree pid;
    Name n;
    int pos;
}
{
    <IMPORT> n=name()   { 
 	   	pos = position();
    	pid = F.at(pos).Ident(n);
     }
    ("." n=name()       { 
	 	   	pos = position(); 
    		pid = F.at(pos).Select(pid, n);
    	} 
    )*
    ";"
        {        	
            return F.at(pos).Import(pid);
        }
}


/**
 * Gets a Name instance from a token.
 */
Name name():
{}
{
    t = <IDENTIFIER> {return Name.fromString(names, t.image); }
}

/**
 * A sequence of definition or declaration of variable and function.
 * <pre>
 * type_declarations_opt   ::= type_declarations   | ;
 * type_declarations ::= 
 *		type_declaration
 *	|	type_declarations type_declaration
 *	
 * type_declaration ::=
 *		Function_declaration
 *	|	variable_declaration (包括结构体和普通变量声明，以及类型重定义typedef)
 *	|	SEMICOLON
 * </pre>
 * @return
 */
List<Tree> top_defs():
{
	
	List<Tree> res;    
    List<Tree> decls = new ArrayList<Tree>();
}
{

	/** a lookahead encountered when reaching a choice point.
	 * 
	 * if next token is belong of set that consists of storage-class, 
	 * type-class, IDENTTIFIER and left parentheses, 
	 * then the definition of function is selected.
	 */ 
    ( 
		res = declaration() { decls.addAll(res); }
    )*
    {
        return decls;
    }
}

List<Tree> declaration():
{
	/**
	 * the definition of various of variables.
	 */   
    Tree methodDef;
    List<Tree> defvars;
    
    List<Tree> defs = new ArrayList<Tree>();
}
{
    LOOKAHEAD( [<STATIC>] type() <IDENTIFIER> "(")
    	methodDef = functionDecl()         { defs.add(methodDef); }
      
    | 
      defvars = variableDefinitions()     { defs.addAll(defvars); }
  	{
  		return defs;
  	}
}


/**
 * A sequence of variable definition.
 * <pre>
 * variable_def ::= 
 *					storage_class_qualifier type_specifier ID
 *				|   storage_class_qualifier type_specifier ID = variable_initializer
 * rariable_initializer ::= expr 						
 * </pre>
 * @return 	
 */
List<Tree> variableDefinitions():
{
	// a list at which a large amounts of various of definition storages. 
    List<Tree> defs = new ArrayList<Tree>();
    long flags = 0;
    long flag = 0;
    Tree type;
    Name name;
    Tree init = null;
    int pos = Position.NOPOS;
}
{
	( 
		t = <STATIC>  
		{ 
			if ( (flags & Flags.STATIC) != 0)
				throw generateParseException();
			else
				flags |= Flags.STATIC;
		}
	  | 
	  	t = <CONST> 
	  	{
			if ( (flags & Flags.CONST) != 0)
				throw generateParseException();
			else
				flags |= Flags.CONST; 
	  	}
	)* 
	{
		if (flags != 0) pos = position();
	}

    type=type() { if (flags == 0) pos = position(); }
    name=name() ["=" init=expr()]
    {
    	VarDef result = F.at(pos).VarDef(flags, name, type, init);
        defs.add(result);
        init = null;
    }
    ( "," name=name() ["=" init=expr()]
        {
    	    result = F.at(pos).VarDef(flags, name, type, init);
		    defs.add(result);
            init = null;
        }
    )* 
    
    ";"
    {
        return defs;
    }
}

/**
 * Handles the initialization portion of variable definition statement.
 */
Tree variableInit() :
{Tree expr;}
{
	LOOKAHEAD ("{") 
	"{"
		{ return arrayInit(null);}
	
	| expr = expr()
	{	
		return expr;
	}
}

/**
  * ArrayInitializer = "{" [VariableInit {"," VariableInit}] [","] "}"
  */
Tree arrayInit(Tree type) :
{
	List<Tree> elems = new ArrayList<Tree>();
	Tree res;
	int pos = 0;
}
{
	 // first, accept a left brace
	"{" { pos = position(); }
	
	// second, parse the optional variable initializer for array.	
	// "," "}" { return F.at(pos).NewArray(type, Tree.emptyList, elems); }
	 
	res = variableInit() 
		{ elems.add(res); }
	( LOOKAHEAD(2)
		"," res = variableInit() { elems.add(res); } 
	)* 
	| "," "}" { return F.at(pos).NewArray(type, Tree.emptyList, elems); }
	
	|"}" { return F.at(pos).NewArray(type, Tree.emptyList, elems); }
	
	"}"	
	{
		return F.at(pos).NewArray(type, Tree.emptyList, elems);
	}	
}

/**
 * A handler to function definition or declaration.
 
 * function_def ::= 
 *				storage_class_qualifier Type Name "(" param_lists_opt ")" block
 * param_lists_opt ::=
 *				| param_lists
 *  
 * param_lists ::=
 *				variable_declaration
 *			|   variable_declaration "," param_list
 
 * @return	A root of AST which represents a MethodDef syntax structure.
 */
Tree functionDecl():
{
    long flags = 0;
    Tree rettype;
    Name methodName;
    List<Tree> params;
    Tree body = null;
    int pos = 0;
}
{
	flags = storage() { if (flags != 0) pos = position(); }  
    rettype = type() { if (flags == 0) pos = rettype.pos; }
    
    methodName = name() params = formalParameters() 
    (
    	body=block()
    	| ";"
    )
    {
        return F.at(pos).MethodDef(flags, methodName, rettype, params, body);
    }
}

/**
 * Identify the storage class qualifier.
 */
long storage():
{
	long flags = 0;
	t = null;
}
{

	[t = <STATIC>] 
	{
		if (t != null)
			return flags |= Flags.STATIC;
		else 
			return flags;
	}
}

/**
 * A function that parses parameters list in the function declaration.
 
 * @return	A list of all of parameters.
 */
List<Tree> formalParameters():
{
    List<Tree> params = new ArrayList<Tree>();
    Tree singleParam;
}
{
	"("
	// check if the next token is "void", determining the return type of
	// function wheterh is void type or not.
    [ 
		singleParam = formalParameter()
			{
				params.add(singleParam);
			}
	  	( "," singleParam = formalParameter()
		    {
			    params.add(singleParam);
		    }
		)*
    ]
    ")"
    {    return params;}
}

/**
 * Parses the single variable declaration statement of function parameter list.
 * 
 * @return
 */
Tree formalParameter():
{
    Tree t;
    Name n;           
}
{
    t = type()
    n = name() 
	{
		return F.at(t.pos).VarDef(Flags.PARAMETER, n, t, null);	
	}
}

/**
 * Parses the function body.
 * <pre>
 * Block_stmt ::=
 *				varibale_defs stmts
 * </pre>
 * @return
 */
Tree block():
{
    int pos;
    List<Tree> caseBody, res = new ArrayList<Tree>();
}
{
    t = "{" { pos = position(); } 
    ( caseBody = blockStatement()
    	{
    		res.addAll(caseBody);
    	}
    )*
    	
    "}"
    {
    	return F.at(pos).Block(res);
    }    
}
/**
 * Just for all statements in block statement.
 */
List<Tree> blockStatement():
{
	List<Tree> globalVariables;
	List<Tree> res = new ArrayList<Tree>();
	Tree stmt;
}
{
	// local variable declaration.
	LOOKAHEAD( type() <IDENTIFIER> )
	globalVariables = variableDefinitions() ";"
		{ res.addAll(globalVariables); }
	| 
	stmt = statement()
		{ res.add(stmt); }
	{
		return res;
	}
}

/**
 * handles to type syntax construct.
 */
Tree type():
{
    Tree ref;
}
{
	// basic type reference, such as void, char, int, long etc.
    ref = baseType()
    ( 
      "[" "]"
        {
        	// array type reference to no length
            ref = F.at(ref.pos).TypeArray(ref);
        }
    )*
    {
        return ref;
    }
}

/**
 * Parses the base type.
 */
Tree baseType():
{
    int pos;
}
{
      t=<VOID>          { pos = position(); return F.at(pos).TypeIdent(Type.VOID); }
    | t=<CHAR>          { pos = position(); return F.at(pos).TypeIdent(Type.CHAR); }
    | t=<SHORT>         { pos = position(); return F.at(pos).TypeIdent(Type.SHORT); }
    | t=<INT>           { pos = position(); return F.at(pos).TypeIdent(Type.INT);  }
    | t=<LONG>          { pos = position(); return F.at(pos).TypeIdent(Type.LONG);  }    
	| t=<BOOL>			{ pos = position(); return F.at(pos).TypeIdent(Type.BOOL);  }
}

/**
 * Parses a kind of various statement, like if statement discussed below.
 * <pre>
 * stmt ::= ;
 *			|labeled_stmt
 *			|expr SEMICOLON
 *			|block
 *			|if_stmt
 *			|while_stmt
 *			|dowhile_stmt
 *			|for_stmt
 *			|switch_stmt
 *			|break_stmt
 *			|continue_stmt
 *			|goto_stmt
 *			|return_stmt;
 * </pre>
 */
Tree statement():
{
	Tree tree = null, expr = null;
}
{
    ( ";"
    | LOOKAHEAD(2) tree = labeledStmt()
    
    | LOOKAHEAD(3) expr = expr() ";" 
    	{ tree = F.at(tree.pos).Exec(expr); }
    | tree = block()
    | tree = ifStmt()
    | tree = whileStmt()
    | tree = doStmt()
    | tree = forStmt()
    | tree = switchStmt()
    | tree = breakStmt()
    | tree = continueStmt()
    | tree = gotoStmt()
    | tree = returnStmt()
    )
    {
        return tree;
    }
}

/**
 * Parses labeled statement.
 */
Tree labeledStmt():
{
    Tree body;
    int pos;
    Name n;
}
{
    n = name() { pos = position(); }
     ":" body = statement()
    {    	
        return F.at(pos).Labelled(n, body);
    }
}

/**
 * Parses if statement.
 */
Tree ifStmt():
{
    Tree cond, thenBody, elseBody = null;
    int pos;
}
{
	// this is a famous problem at c language that "dangling else".
	// It might be solved by lookahead one token.
	
    t = <IF> { pos = position();} 
    "(" cond = expr() ")" thenBody = statement()
            [LOOKAHEAD(1) <ELSE> elseBody = statement()]
        {
            return F.at(pos).If(cond, thenBody, elseBody);
        }
}


/**
 * Parses while statement.
 */
Tree whileStmt():
{
    Tree cond, body;
    int pos;
}
{
    t=<WHILE> {pos = position();} 
    "(" cond=expr() ")" body = statement()
        {
            return F.at(pos).WhileLoop(cond, body);
        }
}

/**
 * Parses do while statement.
 */
Tree doStmt():
{
    Tree cond, body;
    int pos;
}
{
    t = <DO> {pos = position();}
	body = statement()
	<WHILE> "(" cond = expr() ")" 
	";" 
    {
        return F.at(pos).DoLoop(cond, body);
    }
}

/**
 * Parses for statement.
 */
Tree forStmt():
{
	List<Tree> init = null, incr = null;
    Tree body, cond = null;
    int pos;
}
{
    t=<FOR> {pos = position(); } 
    "(" [init = forInit()] ";"
	    [cond = expr() ] ";"
	    [incr = forUpdate()]
    ")" 
    body = statement()
    {
        return F.at(pos).ForLoop(init, cond, incr, body);
    }
}

/**
 * Parses the initialization part of for loop.
 */
List<Tree> forInit():
{
	List<Tree> res;
}
{
	LOOKAHEAD( type() <IDENTIFIER>)
		res = variableDefinitions() { return res; }
	| res = stmtExprs() {return res; }
}

/**
 * Parses the update parts of for statement.
 */
List<Tree> forUpdate():
{
	List<Tree> list;
}
{
	list = stmtExprs()
 	{	return list;}
}
/**
 * Parses expr statement sequences.
 */
List<Tree> stmtExprs():
{
	List<Tree> caseBody = new ArrayList<Tree>();
	Tree res;
}
{
	res = stmtExpr() {caseBody.add(res);}
	(
	"," res = stmtExpr() {caseBody.add(res);}
	)*
	{
		return caseBody;
	}
}

/**
 * The last expansion of this production accepts more than the legal
 * Java expansions for StatementExpression.  This expansion does not
 * use PostfixExpression for performance reasons.
 */
Tree stmtExpr():
{
	Tree expr;
	int pos;
	Tree rhs;
	int op;
}
{
	t = "++" {pos = position(); } expr = unary()
		{ expr = F.at(pos).Unary(Tree.PREINC, expr); }
	|
	t = "--"  {pos = position(); } expr = unary()
		{ expr = F.at(pos).Unary(Tree.PREDEC, expr); }
	|
	expr = primary()
	[
		"++" { expr = F.at(expr.pos).Unary(Tree.POSTINC, expr);}

		| "--" { expr = F.at(expr.pos).Unary(Tree.POSTDEC, expr);}

		| "=" rhs = expr()
			{ expr = F.at(expr.pos).Assign(expr, rhs);}

		| op = opAssign() rhs = expr()
			{ expr = F.at(expr.pos).Assignop(op ,expr, rhs);}
	]
	{
		return expr;
	}
}

/**
 * Parses switch statement.
 */
Tree switchStmt():
{
    int pos;
    Tree selector;
    List<Case> cases;
}
{
    t=<SWITCH> { pos = position(); } 
    "(" selector = expr() ")" 
    "{" cases = switchBlockStatementGroups() "}"
    {
        return F.at(pos).Switch(selector, cases);
    }
}

List<Case> switchBlockStatementGroups():
{
	List<Case> cases = new ArrayList<Case>();
	Tree pat;
	List<Tree> stmts;
	ArrayList<Tree> values = new ArrayList<Tree>();
	int pos;
}
{
    (
        ( t = <CASE> { pos = position(); }
        pat = expr() ":"
            {
                values.add(pat);
            }
        )+
        stmts = blockStatement()
        {
            cases.add(F.at(pos).Case(values, F.at(pos).Block(stmts)));
        }
    )*
    [
        t = <DEFAULT_> { pos = position(); } ":"
            {
            }
        stmts = blockStatement()
        {
            cases.add(F.at(pos).Case(values, F.at(pos).Block(stmts)));
        }
    ]
	{	
		return cases;
	}
}

/**
 * Parses goto statement.
 */
Tree gotoStmt():
{
	Name n;
	int pos;
}
{
    t=<GOTO> {pos = position(); } 
    n = name() ";"
    {
        return F.at(pos).Goto(n);
    }
}

/**
 * Parses break statement.
 */
Tree breakStmt():
{ int pos; }
{
    t=<BREAK> {pos = position(); } 
    ";" 
    { return F.at(pos).Break(); }
}

/**
 * Parses continue statement.
 */
Tree continueStmt():
{ int pos; }
{
    t=<CONTINUE> {pos = position(); } 
    ";" 
    { return F.at(pos).Continue(); }
}

/**
 * Parses return statement.
 */
Tree returnStmt():
{
	Tree expr;
	int pos;
}
{
	t=<RETURN> {pos = position(); }
	";"    
      	{ return F.at(pos).Return(null); }
    | expr = expr() ";"  
    	{ return F.at(expr.pos).Return(expr); }
}

/**
 * Parses expression statement.
 */
Tree expr():
{
    Tree lhs, rhs, expr;
    int op;
}
{
	// assignment statement
      LOOKAHEAD(term() "=")
      lhs = term() "=" rhs = expr()
        {
            return F.at(lhs.pos).Assign(lhs, rhs);
        }
    | LOOKAHEAD(term() opAssign())
      lhs = term() op = opAssign() rhs = expr()
        {
			return F.at(lhs.pos).Assignop(op, lhs, rhs);
        }
        
    // conditional expr
    | expr = expr10()
        {
            return expr;
        }
}


/**
 * Parses operation assignment statement.
 * <p>such as +=, -= , *= and so on.
 */
int opAssign():
{}
{
    ( "+="  { return Tree.PLUS_ASG; }
    | "-="  { return Tree.MINUS_ASG; }
    | "*="  { return Tree.MUL_ASG; }
    | "/="  { return Tree.DIV_ASG; }
    | "%="  { return Tree.MOD_ASG; }
    | "&="  { return Tree.BITAND_ASG; }
    | "|="  { return Tree.BITOR_ASG; }
    | "^="  { return Tree.BITXOR_ASG; }
    | "<<=" { return Tree.SL_ASG; }
    | ">>=" { return Tree.SR_ASG; }
    )
}


/**
 * Parses condition expression.
 */
Tree expr10():
{ Tree cond, truePart, elsePart; }
{
	// other expr or ? : expr.
    cond = expr9() 
    ["?" truePart = expr() ":" elsePart = expr()
		{return F.at(cond.pos).Conditional(cond, truePart, elsePart);}
    ]
    {
        return cond;
    }
}

/**
 * Logical or expression.
 */
Tree expr9():
{ Tree l, r; }
{
    l = expr8() 
    ("||" r = expr8() 
    	{
    		l = F.at(l.pos).Binary(Tree.OR, l, r); 
    	}
    )*
    {
        return l;
    }
}

/**
 * Logical and expr.
 */
Tree expr8():
{ Tree l, r; }
{
    l = expr7() 
    ("&&" r=expr7() 
    	{	l = F.at(l.pos).Binary(Tree.AND, l, r); }
    )*
    {
        return l;
    }
}


/**
 * relational expr.
 */
Tree expr7():
{ Tree l, r; }
{
    l = expr6() ( ">"  r=expr6() { l = F.at(l.pos).Binary(Tree.GT, l, r); }
              | "<"  r=expr6() { l = F.at(l.pos).Binary(Tree.LT, l, r);  }
              | ">=" r=expr6() { l = F.at(l.pos).Binary(Tree.GE, l, r);  }
              | "<=" r=expr6() { l = F.at(l.pos).Binary(Tree.LE, l, r); }
              | "==" r=expr6() { l = F.at(l.pos).Binary(Tree.EQ, l, r); }
              | "!=" r=expr6() { l = F.at(l.pos).Binary(Tree.NE, l, r); } )*
        {
            return l;
        }
}

/**
 * Parses bit or expression.
 */
Tree expr6():
{ Tree l, r; }
{
    l=expr5() ("|" r=expr5() {l = F.at(l.pos).Binary(Tree.BITOR, l, r); })*
    {
        return l;
    }
}


/**
 * Parses exclusive bit or expression.
 */
Tree expr5():
{ Tree l, r; }
{
    l=expr4() ("^" r=expr4() { l = F.at(l.pos).Binary(Tree.BITXOR, l, r); })*
    {
        return l;
    }
}

/**
 * Parses bit and expression.
 */
Tree expr4():
{ Tree l, r; }
{
    l=expr3() ("&" r=expr3() { l = F.at(l.pos).Binary(Tree.BITAND, l, r);})*
    {
        return l;
    }
}

/**
 * Parses right or left shift expression.
 */
Tree expr3():
{ Tree l, r; }
{
    l=expr2() ( ">>" r=expr2() { l = F.at(l.pos).Binary(Tree.SL, l, r); }
              | "<<" r=expr2() { l = F.at(l.pos).Binary(Tree.SR, l, r); }
              )*
        {
            return l;
        }
}


/**
 * Parses plus or minus expr.
 *
 */
Tree expr2():
{ Tree l, r; }
{
    l=expr1() ( "+" r=expr1() {l = F.at(l.pos).Binary(Tree.PLUS, l, r); }
              | "-" r=expr1() {l = F.at(l.pos).Binary(Tree.MINUS, l, r); }
              )*
        {
            return l;
        }
}


/**
 * Parses multiplex or division or modular expr.
 */
Tree expr1():
{ Tree l, r; }
{
    l=term() ( "*" r=term() { l = F.at(l.pos).Binary(Tree.MUL, l, r); }
             | "/" r=term() { l = F.at(l.pos).Binary(Tree.DIV, l, r); }
             | "%" r=term() { l = F.at(l.pos).Binary(Tree.MOD, l, r); }
             )*
        {
            return l;
        }
}


/**
 * Parses type cast or unary expr
 *
 */
Tree term():
{
    Tree type;
    Tree n;
}
{
      LOOKAHEAD("(" primitiveType())
      "(" type = type() ")" n = term()     { return F.at(type.pos).TypeCast(type, n); }
    | n = unary()                     { return n; }
}

/**
 * Currently, there are bool, char, byte, short, int, long, float, double
 * supported as primitive type rather than no aggregate or class.
 */
void primitiveType():
{}
{
  "bool"
|
  "char"
|
  "byte"
|
  "short"
|
  "int"
|
  "long"
|
  "float"
|
  "double"
}

/**
 * Parses unary operation expression.
 */
Tree unary():
{
    Tree tree;
    int pos;
}
{
      t = "++" {pos = position(); }  tree = unary()    
      { return F.at(pos).Unary(Tree.PREINC, tree); }
   
    | t = "--" {pos = position(); }  tree = unary()    
      { return F.at(pos).Unary(Tree.PREDEC, tree); }
      
    | "+" tree = term()      { return tree; }
    
    | t = "-" {pos = position(); }  tree = term()      
    { return F.at(pos).Unary(Tree.NEG, tree); }
    
    | t = "!" {pos = position(); }  tree = term()      
    { return F.at(pos).Unary(Tree.NOT, tree); }
    
    | t = "~" {pos = position(); }  tree = term()      
    { return F.at(pos).Unary(Tree.COMPL, tree); }

    | tree = postfix()       { return tree; }
}


/**
 * Parses unary operation with postfix.
 */
Tree postfix():
{
    Tree expr, idx;
    List<Tree> args;
}
{
    expr = primary()
    
    ( "++"                  { expr = F.at(expr.pos).Unary(Tree.POSTINC, expr); }
    | "--"                  { expr = F.at(expr.pos).Unary(Tree.POSTDEC, expr);}
    | "[" idx = expr() "]"    { expr = F.at(expr.pos).Indexed(expr, idx); }
    | "(" args = args() ")"   { expr = F.at(expr.pos).Apply(expr, args); }
    )*
    
    {
        return expr;
    }
}

/**
 * Parses all of arguments of the calling to function.
 */
List<Tree> args():
{
    List<Tree> args = new ArrayList<Tree>();
    Tree arg;
}
{
    [ arg = expr() { args.add(arg); }
      ("," arg = expr() { args.add(arg); })* 
    ]
    {
        return args;
    }
}


/**
 * Parses literal with primitive, like number literal, character literal etc.
 */
Tree primary():
{
    Tree n;
    int pos;
}
{
    // encounters a integer literal
	t = <INTEGER> { pos = position(); }
        {
        	try 
        	{
            	return integerNode(pos, t.image);
        	}
        	catch( NumberFormatException ex) 
        	{
				log.error(pos, "int.number.too.large", t.image);
        	}
        } 
    | t = <CHARACTER> { pos = position(); }
        {
			return F.at(pos).Literal(Type.CHAR, new Integer(t.image.charAt(0)));
        }
    | t = <IDENTIFIER> { pos = position(); }
        {
			return F.at(pos).Ident(Name.fromString(names, t.image));
        }
    | t = <TRUE_> { pos = position();}
        {
            return F.at(pos).Ident(Name.fromString(names, "true"));
        }
    | t= <FALSE_> { pos = position();} 
        {
            return F.at(pos).Ident(Name.fromString(names, "false"));
        } 
    | "(" n = expr() ")"
        {
            return n;
        }
}
